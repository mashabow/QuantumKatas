{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Superdense Coding Kata\n",
    "\n",
    "**Superdense Coding** quantum kata is a series of exercises designed to get you familiar with programming in Q#.\n",
    "\n",
    "It covers the superdense coding protocol which allows us to transmit two bits of classical information by sending just one qubit using previously shared quantum entanglement.\n",
    "\n",
    " - A good description can be found in [the Wikipedia article](https://en.wikipedia.org/wiki/Superdense_coding).\n",
    " - A great interactive demonstration can be found [on the Wolfram Demonstrations Project](http://demonstrations.wolfram.com/SuperdenseCoding/).\n",
    " - Superdense coding protocol is described in Nielsen & Chuang, section 2.3 (pp. 97-98).\n",
    "\n",
    "Each task is wrapped in one operation preceded by the description of the task. Your goal is to fill in the blank (marked with `// ...` comment) with some Q# code that solves the task. To verify your answer, run the cell using Ctrl/‚åò+Enter.\n",
    "\n",
    "Each task defines an operation that can be used in subsequent tasks to simplify implementations and build on existing code.  We split the superdense coding protocol into several steps, following the description in the [Wikipedia article](https://en.wikipedia.org/wiki/Superdense_coding):\n",
    "\n",
    "* Preparation (creating the entangled pair of qubits that are sent to Alice and Bob).\n",
    "* Encoding the message (Alice's task): Encoding the classical bits of the message into the state of Alice's qubit which then is sent to Bob.\n",
    "* Decoding the message (Bob's task): Using Bob's original qubit and the qubit he received from Alice to decode the classical message sent.\n",
    "* Finally, we compose those steps into the complete superdense coding protocol."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "To begin, first prepare this notebook for execution (if you skip this step, you'll get \"Syntax does not match any known patterns\" error when you try to execute Q# code in the next cells):"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "application/json": "[\"Microsoft.Quantum.Standard::0.10.1911.1607\",\"Microsoft.Quantum.Katas::0.10.1911.1607\"]",
      "text/html": [
       "<ul><li>Microsoft.Quantum.Standard::0.10.1911.1607</li><li>Microsoft.Quantum.Katas::0.10.1911.1607</li></ul>"
      ],
      "text/plain": [
       "Microsoft.Quantum.Standard::0.10.1911.1607, Microsoft.Quantum.Katas::0.10.1911.1607"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "%package Microsoft.Quantum.Katas::0.10.1911.1607"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "> The package versions in the output of the cell above should always match. If you are running the Notebooks locally and the versions do not match, please install the IQ# version that matches the version of the `Microsoft.Quantum.Katas` package.\n",
    "> <details>\n",
    "> <summary><u>How to install the right IQ# version</u></summary>\n",
    "> For example, if the version of `Microsoft.Quantum.Katas` package above is 0.1.2.3, the installation steps are as follows:\n",
    ">\n",
    "> 1. Stop the kernel.\n",
    "> 2. Uninstall the existing version of IQ#:\n",
    ">        dotnet tool uninstall microsoft.quantum.iqsharp -g\n",
    "> 3. Install the matching version:\n",
    ">        dotnet tool install microsoft.quantum.iqsharp -g --version 0.1.2.3\n",
    "> 4. Reinstall the kernel:\n",
    ">        dotnet iqsharp install\n",
    "> 5. Restart the Notebook.\n",
    "> </details>\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Task 1. Entangled pair\n",
    "\n",
    "**Input:** Two qubits, each in the $|0\\rangle$ state.\n",
    "\n",
    "**Goal:**  Prepare a Bell state $|\\Phi^{+}\\rangle = \\frac{1}{\\sqrt{2}} (|00\\rangle + |11\\rangle)$ on these qubits."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "application/json": "\"Success!\"",
      "text/plain": [
       "Success!"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "%kata T1_CreateEntangledPair_Test\n",
    "\n",
    "operation CreateEntangledPair (q1 : Qubit, q2 : Qubit) : Unit is Adj {\n",
    "    H(q1);\n",
    "    CNOT(q1, q2);\n",
    "}"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Task 2. Send the message (Alice's task) \n",
    "\n",
    "Encode the message (two classical bits) in the state of Alice's qubit.\n",
    "\n",
    "**Inputs**: \n",
    "1. Alice's part of the entangled pair of qubits qAlice.\n",
    "2. Two classical bits, stored as ProtocolMessage.\n",
    "\n",
    "**Goal**:  Transform the input qubit to encode the two classical bits.\n",
    "\n",
    ">`ProtocolMessage` is a custom type that represents the message to be transmitted. It includes two items of type `Bool` called `Bit1` and `Bit2`.\n",
    "\n",
    "<br/>\n",
    "<details>\n",
    "  <summary>Need a hint? Click here</summary>\n",
    "Manipulate Alice's half of the entangled pair to change the joint state of the two qubits to one of the following four states based on the value of message:   \n",
    "\n",
    "* [0; 0]: $|\\Phi^{+}\\rangle = \\frac{1}{\\sqrt{2}} (|00\\rangle + |11\\rangle)$\n",
    "* [0; 1]: $|\\Psi^{+}\\rangle = \\frac{1}{\\sqrt{2}} (|01\\rangle + |10\\rangle)$\n",
    "* [1; 0]: $|\\Phi^{-}\\rangle = \\frac{1}{\\sqrt{2}} (|00\\rangle - |11\\rangle)$\n",
    "* [1; 1]: $|\\Psi^{-}\\rangle = \\frac{1}{\\sqrt{2}} (|01\\rangle - |10\\rangle)$\n",
    "\n",
    "</details>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "üìù $00, 01, 10, 11$ „Çí„ÄÅ„Åù„Çå„Åû„Çå $|\\Phi^{+}\\rangle, |\\Psi^{+}\\rangle, |\\Phi^{-}\\rangle, |\\Psi^{-}\\rangle$ „Å´ÂØæÂøú„Å•„Åë„Çã„ÄÇ"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "application/json": "\"Success!\"",
      "text/plain": [
       "Success!"
      ]
     },
     "execution_count": 3,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "%kata T2_EncodeMessageInQubit_Test \n",
    "\n",
    "open Quantum.Kata.SuperdenseCoding;\n",
    "\n",
    "operation EncodeMessageInQubit (qAlice : Qubit, message : ProtocolMessage) : Unit {\n",
    "    if (message::Bit1) { // accesses the item 'Bit1' of 'message'\n",
    "        Z(qAlice);\n",
    "    }\n",
    "    if (message::Bit2) {\n",
    "        X(qAlice);\n",
    "    }\n",
    "}"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Task 3. Decode the message and reset the qubits (Bob's task)\n",
    "\n",
    "Decode the message using the qubit received from Alice and reset both qubits to a $|00\\rangle$ state.\n",
    "\n",
    "**Inputs:**\n",
    "\n",
    "1. Qubit received from Alice qAlice.\n",
    "2. Bob's part of the entangled pair qBob.\n",
    "\n",
    "**Goal** :  Retrieve two bits of classic data from the qubits and return them as `ProtocolMessage`. The state of the qubits in the end of the operation should be $|00\\rangle$.\n",
    "\n",
    "> You can create an instance of `ProtocolMessage` as `ProtocolMessage(bit1value, bit2value)`."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "üìù `CNOT` „Å® `H` „Çí‰Ωø„ÅÜ„Å®„ÄÅ\n",
    "\n",
    "$$\n",
    "\\begin{align}\n",
    "|\\Phi^{+}\\rangle &= \\frac{1}{\\sqrt{2}} (|00\\rangle + |11\\rangle)\n",
    "    \\longrightarrow \\frac{1}{\\sqrt{2}} (|00\\rangle + |10\\rangle)\n",
    "    \\longrightarrow \\frac{1}{2} (|00\\rangle + |10\\rangle + |00\\rangle - |10\\rangle)\n",
    "    = |00\\rangle \\\\\n",
    "|\\Psi^{+}\\rangle &= \\frac{1}{\\sqrt{2}} (|01\\rangle + |10\\rangle)\n",
    "    \\longrightarrow \\frac{1}{\\sqrt{2}} (|01\\rangle + |11\\rangle)\n",
    "    \\longrightarrow \\frac{1}{2} (|01\\rangle + |11\\rangle + |01\\rangle - |11\\rangle)\n",
    "    = |01\\rangle \\\\\n",
    "|\\Phi^{-}\\rangle &= \\frac{1}{\\sqrt{2}} (|00\\rangle - |11\\rangle)\n",
    "    \\longrightarrow \\frac{1}{\\sqrt{2}} (|00\\rangle - |10\\rangle)\n",
    "    \\longrightarrow \\frac{1}{2} (|00\\rangle + |10\\rangle - |00\\rangle + |10\\rangle)\n",
    "    = |10\\rangle \\\\\n",
    "|\\Psi^{-}\\rangle &= \\frac{1}{\\sqrt{2}} (|01\\rangle - |10\\rangle)\n",
    "    \\longrightarrow \\frac{1}{\\sqrt{2}} (|01\\rangle - |11\\rangle)\n",
    "    \\longrightarrow \\frac{1}{2} (|01\\rangle + |11\\rangle - |01\\rangle + |11\\rangle)\n",
    "    = |11\\rangle \\\\\n",
    "\\end{align}\n",
    "$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "application/json": "\"Success!\"",
      "text/plain": [
       "Success!"
      ]
     },
     "execution_count": 8,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "%kata T3_DecodeMessageFromQubits_Test \n",
    "\n",
    "open Quantum.Kata.SuperdenseCoding;\n",
    "open Microsoft.Quantum.Convert;\n",
    "\n",
    "operation DecodeMessageFromQubits (qAlice : Qubit, qBob : Qubit) : ProtocolMessage {\n",
    "    CNOT(qAlice, qBob);\n",
    "    H(qAlice);\n",
    "    \n",
    "    let bit1 = ResultAsBool(M(qAlice));\n",
    "    let bit2 = ResultAsBool(M(qBob));\n",
    "    \n",
    "    // „É™„Çª„ÉÉ„Éà\n",
    "    if (bit1) {\n",
    "        X(qAlice);\n",
    "    }\n",
    "    if (bit2) {\n",
    "        X(qBob);\n",
    "    }\n",
    "\n",
    "    return ProtocolMessage(bit1, bit2);\n",
    "}"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "üìù `ReferenceImplementation.qs` „ÅÆËß£Á≠î‰æã„ÇíË¶ã„Å¶„Åø„Çã„Å®„ÄÅÊúÄÂàù„ÅÆ `CNOT`„Éª`H` „ÅØ Task 1 „ÅÆÈÄÜÊìç‰Ωú„Å™„ÅÆ„Åß„ÄÅ\n",
    "\n",
    "```\n",
    "Adjoint CreateEntangledPair(qAlice, qBob);\n",
    "```\n",
    "\n",
    "„Åå‰Ωø„Åà„Çã„Çâ„Åó„ÅÑ„ÄÇ\n",
    "\n",
    "„Åæ„Åü [`MResetZ`](https://docs.microsoft.com/en-us/qsharp/api/qsharp/microsoft.quantum.measurement.mresetz?view=qsharp-preview) „Çí‰Ωø„ÅÜ„Å®„ÄÅ„ÄåÊ∏¨ÂÆö„Åó„Å¶„É™„Çª„ÉÉ„Éà„Äç„Åå‰∏ÄÂ∫¶„Å´Ë°å„Åà„Çã„Å®„ÅÆ„Åì„Å®„ÄÇ"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Task 4. Superdense coding protocol end-to-end:\n",
    "\n",
    "Put together the steps performed in tasks 1-3 to implement the full superdense coding protocol.\n",
    "\n",
    "**Input:** Two classical bits to be transmitted.\n",
    "\n",
    "**Goal:**  Prepare an EPR Pair, encode the two classical bits in the state of the pair by applying quantum gates to one member of the pair, and decode the two classical bits from the state of the pair. Return the result of decoding."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "üìù „Åì„Çå„Åæ„Åß„ÅÆÊâãÈ†Ü„Çí„Åæ„Å®„ÇÅ„Çã„Å†„Åë„ÄÇ"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "application/json": "\"Success!\"",
      "text/plain": [
       "Success!"
      ]
     },
     "execution_count": 9,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "%kata T4_SuperdenseCodingProtocol_Test\n",
    "\n",
    "open Quantum.Kata.SuperdenseCoding;\n",
    "\n",
    "operation SuperdenseCodingProtocol (message : ProtocolMessage) : ProtocolMessage {\n",
    "    using ((qAlice, qBob) = (Qubit(), Qubit())) {\n",
    "        CreateEntangledPair(qAlice, qBob);\n",
    "        EncodeMessageInQubit(qAlice, message);\n",
    "        return DecodeMessageFromQubits(qAlice, qBob);\n",
    "    }\n",
    "}"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Q#",
   "language": "qsharp",
   "name": "iqsharp"
  },
  "language_info": {
   "file_extension": ".qs",
   "mimetype": "text/x-qsharp",
   "name": "qsharp",
   "version": "0.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
